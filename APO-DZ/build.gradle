buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.2'
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 19
    buildToolsVersion "20"

    defaultConfig {
        applicationId 'org.upennapo.app'
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 14
        versionName "2.2.6"

        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'

        // TODO: use product flavors to clarify resource switching?
        resValue "string", "pledge_directory_sheet_key", "Pledges"
        resValue "string", "brother_directory_sheet_key", "Brothers"
        resValue "string", "alumni_directory_sheet_key", "Alumni"
    }


    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-DEBUG'
            runProguard false

            // Overriding Strings here lets us take screenshots in debug builds without compromising
            // anyone's contact info. Previously we manually went to xml files and changed them back
            // and forth specifically for this purpose.
            resValue "string", "pledge_directory_sheet_key", "DummyPledges"
            resValue "string", "brother_directory_sheet_key", "DummyBrothers"
            resValue "string", "alumni_directory_sheet_key", "DummyAlumni"
        }
        beta {
            versionNameSuffix '-beta'
            runProguard true
        }
        release {
            runProguard true
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')

    // You must install or update the Support Repository through the SDK manager to use this dependency.
    compile 'com.android.support:support-v4:20.0.0'
    compile 'com.google.code.gson:gson:2.3'

    compile 'com.jakewharton:butterknife:5.1.2'
    compile 'com.melnykov:floatingactionbutton:1.0.3'
}
